apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Spring JAX-RS Client Proxy'
                packaging 'jar'
                description 'Spring JAX-RS Client Proxy'
                url 'https://github.com/jmnarloch/spring-jax-rs-client-proxy'

                scm {
                    connection 'scm:git:https://github.com/jmnarloch/spring-jax-rs-client-proxy.git'
                    developerConnection 'scm:git:https://github.com/jmnarloch/spring-jax-rs-client-proxy.git'
                    url 'https://github.com/jmnarloch/spring-jax-rs-client-proxy.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jmnarloch'
                        name 'Jakub Narloch'
                        email 'jmnarloch@gmail.com'
                    }
                }
            }
        }
    }
}

def installer = install.repositories.mavenInstaller
def deployer = uploadArchives.repositories.mavenDeployer

[installer, deployer]*.pom*.whenConfigured {pom ->
    pom.dependencies.findAll {dep -> dep.scope == 'compile'}*.optional = true
}